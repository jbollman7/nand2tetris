// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // chooses arithmetic or logic unit
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0
// stage 1, ignore zr and ng
// stage 2, only after stage 1 tests pass address
// zr and ng
    PARTS:

    // handle zx
    Mux16(a=x, b=false, sel=zx, out=m1 );

    // Handle nx - negate X use m1 for input
    Not16(in=m1, out=tmpx);
    Mux16(a=m1, b=tmpx, sel=nx, out=invx);

    // Handle zy
    Mux16(a=y, b=false, sel=zy , out=m2);


    // Handle ny - negate Y use m2 for input
    Not16(in=m2, out=tmpy);
    Mux16(a=m2, b=tmpy, sel=ny, out=invy );

    


    // Build out arithmetic unit
    Add16(a=invx, b=invy, out=arithmetic);

    // Build out logic unit
    And16(a=invx, b=invy , out=logic );
   
    // Determine arithmetic or logic
    Mux16(a=logic, b=arithmetic, sel=f, out=a1);

    // bitwise not for output
    Not16(in=a1, out=negateOut);
    Mux16(a=a1, b=negateOut, sel=no, out=out, out[0..7]=final07, out[8..15]=final815, out[15]=isNeg );

   // Handle zr perfectly

    Or8Way(in=final07, out=p );
    Or8Way(in=final815, out=q );
    Or(a=p, b=q , out=isOutZero );
    Mux(a=true, b=false, sel=isOutZero , out=zr );
    
    // Hangle ng 
    And(a=isNeg, b=true, out=ng );

}